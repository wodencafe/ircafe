import org.panteleyev.jpackage.ImageType

plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.panteleyev.jpackageplugin' version '1.7.6'
}

group = 'cafe.woden'

version = (findProperty("version") ?: "0.0.1-SNAPSHOT").toString()

description = 'IRC Client in modern Java, utilizing PircBotX, Spring Boot, and RXJava.'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

springBoot {
    mainClass = 'cafe.woden.ircclient.IrcSwingApp'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'

    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.flywaydb:flyway-core'
    runtimeOnly 'org.flywaydb:flyway-database-hsqldb'
    runtimeOnly 'org.hsqldb:hsqldb'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    implementation 'org.yaml:snakeyaml'
    implementation 'com.fasterxml.jackson.core:jackson-databind'

    implementation 'io.reactivex.rxjava3:rxjava:3.1.12'
    implementation 'com.github.pircbotx:pircbotx:2.3.1'

    implementation "io.github.andrewauclair:modern-docking-api:1.4.1"
    implementation "io.github.andrewauclair:modern-docking-single-app:1.4.1"
    implementation "io.github.andrewauclair:modern-docking-ui:1.4.1"

    implementation "com.formdev:flatlaf:3.7"
    implementation "com.formdev:flatlaf-extras:3.7"

    implementation "com.fifesoft:autocomplete:3.3.3"

    implementation "org.sejda.imageio:webp-imageio:0.1.6"
    implementation "com.madgag:animated-gif-lib:1.4"

    implementation "com.miglayout:miglayout-swing:11.4.2"

    implementation "org.jsoup:jsoup:1.22.1"

    // Better tray support on Linux/GNOME (AppIndicator/GtkStatusIcon fallback) + native menus.
    // NOTE: Maven Central currently publishes 4.4 (README mentions 4.5, but it is not on Central).
    implementation "com.dorkbox:SystemTray:4.4"

    // Cross-platform desktop notification popups (toast-like). Used for Windows notifications.
    implementation "com.dorkbox:Notify:4.5"

    // DBus (Linux/GNOME) - used to implement clickable desktop notifications via org.freedesktop.Notifications.
    implementation "com.github.hypfvieh:dbus-java-core:5.2.0"
    implementation "com.github.hypfvieh:dbus-java-transport-native-unixsocket:5.2.0"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('bootJar') {
    archiveFileName = "ircafe-${project.version}.jar"
}
tasks.named('jar') {
    archiveClassifier = "plain"
}

/**
 * ---------------------------
 * jpackage input preparation
 * ---------------------------
 *
 * We copy:
 *  - the plain jar (task 'jar' output)
 *  - runtime dependencies
 * into build/jpackage/input
 */
def jpackageInputDir = layout.buildDirectory.dir("jpackage/input")

def packagingRuntimeClasspath = configurations.findByName("productionRuntimeClasspath") ?: configurations.runtimeClasspath

tasks.register("copyJpackageDependencies", Copy) {
    from(packagingRuntimeClasspath)
    into(jpackageInputDir)
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    exclude { details ->
        details.file.name.contains("spring-boot-devtools")
    }
}

tasks.register("copyJpackageJar", Copy) {
    dependsOn tasks.named("jar")
    from(tasks.named("jar").map { it.archiveFile })
    into(jpackageInputDir)
}

tasks.named("jpackage") {
    dependsOn("assemble", "copyJpackageDependencies", "copyJpackageJar")
}

String sanitizeAppVersion(String v) {
    if (v == null) return "0.0.0"
    v = v.replaceFirst(/^v/, "")
         .replace("-SNAPSHOT", "")
         .trim()

    v = v.replaceAll(/[^0-9.].*$/, "")

    if (v.isEmpty()) return "0.0.0"
    if (!(v ==~ /\d+(\.\d+){0,3}/)) return "0.0.0"
    return v
}

jpackage {
    input = jpackageInputDir
    destination = layout.buildDirectory.dir("dist")

    appName = "IRCafe"
    vendor = "WodenCafe"
    appDescription = project.description

    appVersion = sanitizeAppVersion(project.version.toString())

    mainJar = tasks.named("jar").get().archiveFileName.get()
    mainClass = springBoot.mainClass.get()

    type = ImageType.APP_IMAGE

    verbose = true
    temp = "${buildDir}/jpackage/tmp"

    javaOptions = [
        "-Dfile.encoding=UTF-8"
    ]

    windows {
        winConsole = false
    }

    mac {
        macPackageIdentifier = "cafe.woden.ircafe"
        macPackageName = "IRCafe"
        macAppCategory = "public.app-category.utilities"
    }
}
